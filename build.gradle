plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group 'com.github.itechbear'
version '0.0.1'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

compileJava {
    options.compilerArgs += ["-encoding", "utf-8"]
    options.fork = true
    options.forkOptions.executable = "javac"
}

dependencies {
    testImplementation("org.junit.jupiter:junit-jupiter-api:${jupiterVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${jupiterVersion}")
}

test {
    useJUnitPlatform()
}

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = "robotstxt"
            from components.java

            artifact(sourceJar) {
                classifier = 'sources'
            }
            artifact(javadocJar) {
                classifier = 'javadoc'
            }

            pom {
                name = "robotstxt"
                description = "A java clone of Google's robotst.txt parser: https://github.com/google/robotstxt"
                url = "https://github.com/itechbear/robotstxt"
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'itechbear'
                        name = 'Hu Dong'
                        email = 'itechbear@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/itechbear/robotstxt.git'
                    developerConnection = 'scm:git:ssh://github.com/itechbear/robotstxt.git'
                    url = "https://github.com/itechbear/robotstxt"
                }
            }
        }
    }
    repositories {
        maven {
            // url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            url "https://oss.sonatype.org/content/repositories/snapshots"
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
